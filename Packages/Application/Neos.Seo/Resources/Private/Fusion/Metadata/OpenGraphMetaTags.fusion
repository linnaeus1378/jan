prototype(Neos.Seo:OpenGraphMetaTags) < prototype(Neos.Fusion:Component) {
    type = ${q(node).property('openGraphType') || 'website'}
    title = ${q(node).property('openGraphTitle') || q(node).property('titleOverride') || q(node).property('title')}
    description = ${q(node).property('openGraphDescription') || q(node).property('metaDescription')}
    siteName = ${q(site).property('titleOverride') || site.label}
    locale = Neos.Seo:LangAttribute
    url = Neos.Neos:NodeUri {
        node = ${node}
        absolute = true
    }
    image = Neos.Fusion:Case {
        default {
            @position = 'start'
            condition = ${Type.instance(q(node).property('openGraphImage'), 'Neos\Media\Domain\Model\ImageInterface')}
            renderer = ${q(node).property('openGraphImage')}
        }
    }
    imageAltText = ${this.image.caption || this.image.label}

    preset = ${'Neos.Seo:OpenGraph.' + (this.image.orientation == 'landscape' ? 'Landscape' : 'Square')}
    preset.@if.hasImage = ${this.image}
    thumbnail = ${Neos.Seo.Image.createThumbnail(this.image, this.preset)}
    thumbnail.@if.hasImage = ${this.image}

    renderer = afx`
        <meta @key="type" property="og:type" content={props.type}/>
        <meta @key="title" property="og:title" content={props.title}/>
        <meta @key="siteName" property="og:site_name" content={props.siteName}/>
        <meta @key="locale" property="og:locale" content={props.locale} @if.set={props.locale}/>
        <meta @key="description" property="og:description" content={props.description} @if.set={props.description}/>
        <meta @key="url" property="og:url" content={props.url}/>
        <Neos.Fusion:Join @key="image" @if.set={props.image}>
            <meta @key="image" property="og:image">
                <Neos.Fusion:ResourceUri resource={props.thumbnail.resource} @path='attributes.content'/>
            </meta>
            <meta @key="width" property="og:image:width" content={props.thumbnail.width}/>
            <meta @key="height" property="og:image:height" content={props.thumbnail.height}/>
            <meta @key="alt" property="og:image:alt" content={props.imageAltText} @if.set={props.imageAltText}/>
        </Neos.Fusion:Join>
    `
}
