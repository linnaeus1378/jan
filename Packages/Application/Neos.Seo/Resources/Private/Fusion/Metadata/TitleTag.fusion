prototype(Neos.Seo:TitleTag) < prototype(Neos.Fusion:Component) {
    suffix = ${site.context.currentSite.name}
    titleOverride = ${q(node).property('titleOverride')}
    breadcrumbSeparator = ' - '
    suffixSeparator = ${this.breadcrumbSeparator}
    outputSuffixOnTitleOverride = true

    renderSuffix = ${this.suffix && (this.titleOverride ? this.outputSuffixOnTitleOverride : true)}

    @context {
        breadcrumbSeparator = ${this.breadcrumbSeparator}
        titleOverride = ${this.titleOverride}
    }

    title = Neos.Fusion:Case {
        titleOverride {
            condition = ${titleOverride}
            renderer = ${titleOverride}
            @position = 'start'
        }

        default {
            condition = true
            @position = 'end'
            renderer = Neos.Fusion:Loop {
                // Retrieve all parent document nodes excluding the homepage
                items = ${q(documentNode).add(q(documentNode).parents('[instanceof Neos.Neos:Document]')).get()}
                items.@process.removeSiteNode = ${Array.slice(value, 0, -1)}
                items.@process.removeSiteNode.@if.onSubPage = ${documentNode != site}
                // Implode node titles with the separator
                itemRenderer = ${q(item).property('title') + (iterator.isLast ? '' : breadcrumbSeparator)}
            }
        }

        @process.stripTags = ${String.stripTags(value)}
    }

    renderer = afx`
        <title>
            {props.title}
            {props.title && props.renderSuffix ? props.suffixSeparator : ''}
            {props.renderSuffix ? props.suffix : ''}
        </title>
    `
}
