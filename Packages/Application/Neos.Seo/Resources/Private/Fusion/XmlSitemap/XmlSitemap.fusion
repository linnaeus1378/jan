prototype(Neos.Seo:XmlSitemap) < prototype(Neos.Fusion:Http.Message) {
    doctype = '<?xml version="1.0" encoding="UTF-8"?>'
    httpResponseHead.headers.Content-Type = 'text/xml'

    body = Neos.Fusion:Component {
        includeAlternateLanguageLinks = false
        startingPoint = ${site}
        includeImageUrls = false

        renderer = afx`
            <urlset xmlns="http://www.sitemaps.org/schemas/sitemap/0.9"
                    xmlns:image="http://www.google.com/schemas/sitemap-image/1.1"
                    xmlns:xhtml="http://www.w3.org/1999/xhtml">
                <Neos.Seo:XmlSitemap.UrlList
                    @if.hasItems={this.items}
                    @children='items'
                    includeAlternateLanguageLinks={props.includeAlternateLanguageLinks}>
                    <Neos.Seo:Helper.SitemapUrls startingPoint={props.startingPoint} includeImageUrls={props.includeImageUrls} />
                </Neos.Seo:XmlSitemap.UrlList>
            </urlset>
        `

        @context.startingPoint = ${this.startingPoint}        
        @cache {
            mode = 'cached'
            entryIdentifier {
                startingPoint = ${startingPoint}
            }
            entryTags {
                1 = ${Neos.Caching.nodeTag(startingPoint)}
                2 = ${Neos.Caching.descendantOfTag(startingPoint)}
            }
        }
    }
}

root.xmlSitemap {
    condition = ${request.format == 'xml.sitemap'}
    type = 'Neos.Seo:XmlSitemap'
}
